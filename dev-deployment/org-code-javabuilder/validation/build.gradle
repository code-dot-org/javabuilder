plugins {
    id 'java'
    id 'com.github.sherter.google-java-format' version '0.8'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation project(':protocol')
    implementation project(':neighborhood')
    // https://mvnrepository.com/artifact/org.json/json
    implementation group: 'org.json', name: 'json', version: '20210307'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation testFixtures(project(':protocol'))
}

test {
    useJUnitPlatform()
}

// Builds the Validation fat-jar (i.e. jar with all dependencies) and copies it to the resources
// folder in the javabuilder sub-project (currently named 'lib'). This will allow student code to
// access the Validation package.
task buildValidation(type: Jar) {
    archiveClassifier = 'full'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    destinationDirectory = project(':lib').project.layout.projectDirectory.dir('src/main/resources/jars')
}

build.dependsOn buildValidation
